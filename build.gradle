buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.2'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

project.group = 'com.allogy.android'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName version
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.github.tony19:logback-android-core:1.0.10-2'
    compile 'com.github.tony19:logback-android-classic:1.0.10-2'
    compile('com.crashlytics.sdk.android:crashlytics:2.0.0@aar') {
        transitive = true;
    }
}

android.libraryVariants.all { variant ->

    def javadocName = "generate${variant.name.capitalize()}Javadoc"
    def javadocJarName = "package${variant.name.capitalize()}JavadocJar"

    task(javadocName, type: Javadoc) {
        title = 'Logback Crashlytics'
        source = variant.javaCompile.source
        classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath())
        options {
            links 'http://tony19.github.io/logback-android/doc/1.0.10-2', 'http://docs.oracle.com/javase/7/docs/api/'
            linksOffline 'http://d.android.com/reference', "${android.sdkDirectory}/docs/reference"
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }

    task(javadocJarName, type: Jar, dependsOn: javadocName) {
        classifier = 'javadoc'
        from tasks.getByName(javadocName).destinationDir
    }

    artifacts {
        if(variant.name == 'release')
            archives tasks.getByName(javadocJarName)
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name 'Logback Crashlytics'
                description 'Provides a logback appender to log to Crashlytics.'
                url 'https://github.com/Allogy/logback-crashlytics'

                organization {
                    name 'Allogy Interactive LLC'
                    url 'http://www.allogy.com'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        comments 'Copyright 2013 Allogy Interactive LLC.'
                    }
                }
                scm {
                    connection 'scm:git:git@github.com:Allogy/logback-crashlytics.git'
                    developerConnection 'scm:git:git@github.com:Allogy/logback-crashlytics.git'
                    url 'https://github.com/Allogy/logback-crashlytics'

                }
                developers {
                    developer {
                        id 'dvenable'
                        name 'David Venable'
                        email 'dvenable@allogy.com'
                    }
                }
            }
        }
    }
}
